if (rootProject.ext.isAlone) {
  apply plugin: 'com.android.application'
} else {
  apply plugin: 'com.android.library'
}

android {
  compileSdkVersion rootProject.ext.android.compileSdkVersion

  defaultConfig {
    if (rootProject.ext.isAlone) {
      applicationId "com.syh.deeepoch.deeec"
    }
    minSdkVersion rootProject.ext.android.minSdkVersion
    targetSdkVersion rootProject.ext.android.targetSdkVersion
    versionCode rootProject.ext.android.versionCode
    versionName rootProject.ext.android.versionName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"


    if (!rootProject.ext.isAlone) {
      javaCompileOptions {
        annotationProcessorOptions {
          arguments = [moduleName: project.getName()]
        }
      }
    }

    vectorDrawables.useSupportLibrary = true
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  sourceSets {
    main {
      if (rootProject.ext.isAlone) {
        manifest.srcFile 'src/main/m_ghost/AndroidManifest.xml'
        java.srcDirs = ['src/main/java', 'src/main/m_ghost/java']
        res.srcDirs = ['src/main/res', 'src/main/m_ghost/res']
      } else {
        manifest.srcFile 'src/main/AndroidManifest.xml'
      }
    }
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation 'com.android.support.constraint:constraint-layout:1.0.2'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'com.android.support.test:runner:1.0.1'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

  implementation project(':epochcore')

  if (!rootProject.ext.isAlone) {
    annotationProcessor "com.alibaba:arouter-compiler:$rootProject.ext.libsVersion.arouter_compiler"
  }
}
